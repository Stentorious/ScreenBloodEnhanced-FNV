int iBitMask
float fDamage
ref rTarget
ref rWeapon

begin Function {}

	; Validate ref, cooldown, creature type, distance
	if eval IsReference (rTarget = This) == 0 || (rTarget == Player && Goo1.AuxVarGetFlt "*ScrBld_INI" 13 != 2) || (rTarget != Player && Goo1.AuxVarGetFlt "*ScrBld_INI" 14 == 0) || Goo1.AuxVarGetFlt "*ScrBld_Timer" 1 || (rTarget.GetCreatureType == 6 && Goo1.AuxVarGetFlt "*ScrBld_INI" 7 == 0) || Player.GetDistance3D rTarget >= ((Goo1.AuxVarGetFlt "*ScrBld_INI" 0) * 69.99104)
		return
	endif

	; Overlay check
	if eval Goo1.AuxVarGetFlt "*ScrBld_INI" 8 && GetUIFloatAlt "HUDMainMenu\HelmetOverlay\Helmet\visible" != 1 && GetUIFloatAlt "HUDMainMenu\HelmetOverlay\Glasses\visible" != 1 && GetUIFloatAlt "HUDMainMenu\ModernNightVision\_AlphaOverlay" != 1
		return
	endif

	; Attack penetrates armor
	if eval rTarget.GetArmourPenetrated
		fDamage = rTarget.GetHitHealthDamage
	endif

	; On first hit in the frame
	if eval Goo1.AuxVarGetFlt "*ScrBld_Timer" 0 == 0
		Goo1.AuxVarSetFlt "*ScrBld_Timer" 1 0
		Goo1.AuxVarErase "*ScrBld_Cache"
		Goo1.AuxVarSetRef "*ScrBld_Cache" rTarget 0
		Goo1.AuxVarSetFlt "*ScrBld_Cache" (rTarget.GetDead) 1
		CallAfterFrames 0 (CompileScript "ScreenBloodEnhanced\SplatterApply.gek")
	endif

	Goo1.AuxVarSetFlt "*ScrBld_Cache" ((Goo1.AuxVarGetFlt "*ScrBld_Cache" 2) + fDamage) 2							; Damage dealt
	Goo1.AuxVarSetFlt "*ScrBld_Cache" ((Goo1.AuxVarGetFlt "*ScrBld_Cache" 3) + (rTarget.GetHitExtendedFlag 3)) 3	; Limb dismembered
	Goo1.AuxVarSetFlt "*ScrBld_Cache" ((Goo1.AuxVarGetFlt "*ScrBld_Cache" 4) + (rTarget.GetHitExtendedFlag 4)) 4	; Limb exploded

	if eval IsBaseForm (rWeapon = rTarget.GetHitWeapon)
		Goo1.AuxVarSetRef "*ScrBld_Cache" rWeapon 5
	endif

end
