int iType
int iCorpse
float fDamage
float fDamageMin
float fDamageMax
float fDamageMul
float fDistance
float fDistanceMax
float fHeading
float fHeadingMax
float fSplatter
float fSize
float fOpacity
float fDuration
ref rTarget
ref rWeapon
ref rExpIMOD
string_var sAlpha
string_var sFlare
string_var sColor

begin Function {}

	Goo1.AuxVarSetFlt "*ScrBld_Timer" 0 0

	; Validate ref
	if eval IsReference (rTarget = Goo1.AuxVarGetRef "*ScrBld_Cache" 0) == 0
		return
	endif

	; Validate POV
	iType = IsPC1stPerson
	if eval (Goo1.AuxVarGetFlt "*ScrBld_INI" 5 == 0 && iType == 1) || (Goo1.AuxVarGetFlt "*ScrBld_INI" 12 == 0 && iType == 0)
		return
	endif

	; Hide corpse splatter
	if eval (iCorpse = Goo1.AuxVarGetFlt "*ScrBld_Cache" 1) && Goo1.AuxVarGetFlt "*ScrBld_INI" 6 == 0
		return
	endif

	; Weapon blacklist
	rWeapon = Goo1.AuxVarGetRef "*ScrBld_Cache" 5
	if eval GetWeaponHasFlag 14 rWeapon
		return
	endif

	; Splatter textures
	sAlpha = "Textures\Stentorious\ScreenBloodEnhanced\Alpha.dds"
	sFlare = "Textures\Stentorious\ScreenBloodEnhanced\Flare.dds"
	sColor = "Textures\Stentorious\ScreenBloodEnhanced\Color_"

	if eval	rTarget.GetCreatureType == 6
		sColor += "Blue.dds"
	elseif eval (iType = rTarget.GetImpactMaterialType) == 11
		sColor += "Green.dds"
		rExpIMOD = BloodGlowISFX
	elseif eval iType == 10
		sColor += "Yellow.dds"
		rExpIMOD = BloodISFXd
	else
		sColor += "Red.dds"
		rExpIMOD = BloodISFXd
		if eval Goo1.AuxVarGetFlt "*ScrBld_SPL" 13
			sAlpha = "Textures\gore\screenbloodalpha01opt.dds"
			sColor = "Textures\gore\screenbloodcolor01opt.dds"
		endif
	endif

	; Damage calculation
	fDamageMin = Goo1.AuxVarGetFlt "*ScrBld_INI" 3
	fDamageMax = (Goo1.AuxVarGetFlt "*ScrBld_INI" 4) - fDamageMin
	fDamage = (Goo1.AuxVarGetFlt "*ScrBld_Cache" 2) - fDamageMin

	; Auto melee fix
	if eval (iType = GetWeapType rWeapon) < 3 && GetWeaponIsAutomatic rWeapon
		fDamage = fDamageMax / 2
	endif

	if eval fDamage < 0
		fDamageMul = 0
	elseif eval fDamageMax == 0
		fDamageMul = 1
	else
		fDamageMul = GetMinOf 1 (fDamage / fDamageMax)
	endif

	; Default splatter count
	fSplatter = Goo1.AuxVarGetFlt "*ScrBld_SPL" 0

	; Corpse hit
	if eval iCorpse
		if eval iType == 0
			fSplatter *= Goo1.AuxVarGetFlt "*ScrBld_SPL" 4
		elseif eval iType < 3 || iType == 13
			fSplatter *= Goo1.AuxVarGetFlt "*ScrBld_SPL" 3
		else
			fSplatter *= Goo1.AuxVarGetFlt "*ScrBld_SPL" 2
		endif

	; Alive/on death hit
	else
		if eval rTarget.IsLimbGone 0
			fSplatter *= Goo1.AuxVarGetFlt "*ScrBld_SPL" 7	; Body exploded (Bloody mess)
		elseif eval rTarget.IsLimbGone 0 13
			if eval Goo1.AuxVarGetFlt "*ScrBld_Cache" 4 > 0
				fSplatter *= Goo1.AuxVarGetFlt "*ScrBld_SPL" 6	; Limb exploded
			else
				fSplatter *= Goo1.AuxVarGetFlt "*ScrBld_SPL" 5	; Limb dismembered
			endif
		elseif eval iType == 0
			fSplatter *= Goo1.AuxVarGetFlt "*ScrBld_SPL" 4	; Unarmed
		elseif eval iType < 3 || iType == 13
			fSplatter *= Goo1.AuxVarGetFlt "*ScrBld_SPL" 3	; Melee & Thrown
		else
			fSplatter *= Goo1.AuxVarGetFlt "*ScrBld_SPL" 2	; Projectile
		endif
	endif

	; Settings
	fDistanceMax = (Goo1.AuxVarGetFlt "*ScrBld_INI" 0) * 69.99104
	fHeadingMax = Goo1.AuxVarGetFlt "*ScrBld_INI" 1

	; Position calculation
	if eval rTarget == Player
		fDistance = 1
		fHeading = 1
	else
		fDistance = (GetMaxOf 0 (fDistanceMax - (Player.GetDistance3D rTarget))) / fDistanceMax
		fHeading = (GetMaxOf 0 (fHeadingMax - (Abs (Player.GetHeadingAngle rTarget)))) / fHeadingMax
	endif

	; Trigger splatter
	if eval fDistance > 0 && fHeading > 0

		; Splatter strength
		fSplatter *= fDamageMul
		fSplatter *= ApplyEasingAlt fDistance 2 0
		fSplatter = Flr ((fSplatter * (ApplyEasingAlt fHeading 2 1)) + 0.5)
		if eval fSplatter > 0

			; Splatter duration
			fDuration = Goo1.AuxVarGetFlt "*ScrBld_SPL" 1
			if eval GetUIFloatAlt "HUDMainMenu\ModernNightVision\_AlphaOverlay" > 0 || GetUIFloatAlt "HUDMainMenu\HelmetOverlay\Helmet\visible" > 0 || GetUIFloatAlt "HUDMainMenu\HelmetOverlay\Glasses\visible" > 0
				fDuration = Goo1.AuxVarGetFlt "*ScrBld_SPL" 14
			endif

			; Splatter size
			fSize = Rand (Goo1.AuxVarGetFlt "*ScrBld_SPL" 9) (Goo1.AuxVarGetFlt "*ScrBld_SPL" 10)

			; Splatter opacity
			fOpacity = Rand (Goo1.AuxVarGetFlt "*ScrBld_SPL" 11) (Goo1.AuxVarGetFlt "*ScrBld_SPL" 12)

			TriggerScreenSplatterEx fSplatter fDuration fSize fOpacity (sAlpha) (sColor) (sFlare) 0

			; Update splatter count
			Player.AuxTimerStart "*ScrBld_Count" fDuration 14
			Goo1.AuxVarSetFlt "*ScrBld_Count" (Goo1.AuxVarGetFlt "*ScrBld_Count" + fSplatter)

			; Splatter cooldown
			CallAfter (Goo1.AuxVarGetFlt "*ScrBld_INI" 2) (CompileScript "ScreenBloodEnhanced\SplatterReset.gek")
			Goo1.AuxVarSetFlt "*ScrBld_Timer" 1 1

		endif
	endif

	; Apply limb explosion IMOD
	if eval IsFormValid rExpIMOD && iCorpse == 0 && Goo1.AuxVarGetFlt "*ScrBld_SPL" 8 && Goo1.AuxVarGetFlt "*ScrBld_Cache" 4 > 0 && rTarget.IsLimbGone 0 13 && rTarget.GetDead
		IMOD rExpIMOD (ApplyEasingAlt fDistance 2 0)
	endif

	sv_Destruct sColor sAlpha sFlare

end